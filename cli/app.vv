import uwu.cli { App }
import uwu

fn play() ! {
	mut app := App.new(
		brief: 'A demo app using the `uwu.cli` module'
		notes: [
			'This is a sample note',
			'Lorem ipsum dolor sit amet, consectetur adipiscing elit, sed do eiusmod tempor incididunt ut labore et dolore magna aliqua',
		]
		author: cli.Author{
			name: 'Alejandro El√≠'
			email: 'dev@onerbs.com'
			website: 'https://onerbs.com'
		}
	)

	a_item := app.item(
		name: 'ahoy'
		value: 'no value'
		is_optional: false
	)
	s_item := app.item(
		name: 'some'
		value: 'no value'
	)

	m_flag := app.flag(
		brief: 'A dummy flag that captures one value'
		name: 'dummy'
		alias: `m`
		value: 'dummy value'
	)
	t_flag := app.int_flag(
		brief: 'A dummy flag that captures an integer'
		name: 'time'
		alias: `t`
		item: 'minutes'
	)
	w_flag := app.flag(
		brief: 'some wide flag'
		alias: `w`
		name: 'wide'
		is_wide: true
	)

	// app.parse_args(['--help'])!
	app.parse_args(['-t', '3', 'ahoy', '-m', 'my message', 'b', 'c', '-w', 'd', 'e'])!

	assert a_item.value() == 'ahoy'
	assert s_item.value() == 'b'
	assert t_flag.int_value() == 3
	assert m_flag.value() == 'my message'
	assert w_flag.values() == ['d', 'e']
	assert app.get_args() == ['c']

	println('args: ${app.get_args()}')
	println('ahoy: ${a_item.value()}')
	println('some: ${s_item.value()}')
	println('-t: ${t_flag.int_value()}')
	println('-m: ${m_flag.value()}')
	println('-w: ${w_flag.values()}')
	println('${*m_flag}')
}

fn main() {
	play() or {
		uwu.catch(err)
	}
}
