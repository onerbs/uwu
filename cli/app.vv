import uwu.cli
import uwu

fn play() ! {
	mut app := cli.new_app(
		brief: 'A demo app using the `uwu.cli` module'
		with_stdin: false
		notes: [
			'This is a sample note',
		]
		author: 'Alejandro El√≠ <dev@onerbs.com>'
	)

	m_flag := app.flag(
		brief: 'A dummy flag that captures one value'
		name: 'dummy'
		alias: `m`
		value: 'dummy value'
	)
	t_flag := app.int_flag(
		brief: 'A dummy flag that captures an integer'
		name: 'time'
		alias: `t`
		item: 'minutes'
	)
	w_flag := app.flag(
		brief: 'some wide flag'
		alias: `w`
		wide: true
	)

	app.parse_args(['-t', '3', 'ahoy', '-m', 'my message', 'b', 'c', '-w', 'd', 'e'])!

	assert app.get_args() == ['ahoy', 'b', 'c']
	assert t_flag.int_value() == 3
	assert m_flag.value() == 'my message'
	assert w_flag.values() == ['d', 'e']

	println('args: ${app.get_args()}')
	println('-m: ${m_flag.value()}')
	println('-t: ${t_flag.int_value()}')
	println('-w: ${w_flag.values()}')
	println('${*t_flag}')
	println(app)
}

fn main() {
	play() or {
		uwu.catch(err)
	}
}
